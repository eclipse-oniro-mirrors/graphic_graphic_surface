# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/graphic/graphic_surface/graphic_surface_config.gni")

## Build sync_fence.so {{{
config("sync_fence_config") {
  visibility = [ ":sync_fence" ]

  cflags = [
    "-Wall",
    "-Werror",
    "-g3",
    "-Wno-error=missing-braces",
    "-Wno-error=#warnings",
  ]
  defines = rs_common_define
}

config("sync_fence_public_config") {
  include_dirs = [
    "include",
    "//foundation/graphic/graphic_surface/interfaces/inner_api/utils",
  ]
}

ohos_shared_library("sync_fence") {
  sanitize = {
    boundary_sanitize = true
    integer_overflow = true
    ubsan = true
  }

  sources = [
    "src/acquire_fence_manager.cpp",
    "src/frame_sched.cpp",
    "src/sync_fence.cpp",
    "src/sync_fence_timeline.cpp",
    "src/sync_fence_tracker.cpp",
  ]

  configs = [ ":sync_fence_config" ]

  public_configs = [ ":sync_fence_public_config" ]

  external_deps = [
    "c_utils:utils",
    "eventhandler:libeventhandler",
    "hilog:libhilog",
    "hisysevent:libhisysevent",
    "hitrace:hitrace_meter",
    "init:libbegetutil",
    "ipc:ipc_single",
  ]

  innerapi_tags = [
    "chipsetsdk",
    "platformsdk_indirect",
  ]

  if (!is_emulator && !build_ohos_sdk && current_os == "ohos") {
    defines = [ "FENCE_SCHED_ENABLE" ]
  }

  part_name = "graphic_surface"
  subsystem_name = "graphic"
}

group("test") {
  testonly = true

  deps = [ "test:test" ]
}
## Build sync_fence.so }}}
